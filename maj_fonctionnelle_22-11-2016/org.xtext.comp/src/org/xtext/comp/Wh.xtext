grammar org.xtext.comp.Wh with org.eclipse.xtext.common.Terminals

generate wh "http://www.xtext.org/comp/Wh"

Wh :
    (elements+=Program)*;
  
 
Program: 
	'function' name=SYMBOL ':' definition=Definition 
;

Definition:
	'read' input=Input '%' command=Commands '%' 'write' output=Output
;

Input:
	vars+=VARIABLE (',' vars+=VARIABLE)*
;

Output:
	vars+=VARIABLE (',' vars+=VARIABLE)*
;

Commands:
	commands+=Command (';' commands+=Command)*
;

Command:
   cmd=(Nop | Affect | If | While | For | Foreach)
;


Foreach:
	('foreach' expr1=Expr 'in' expr2=Expr 'do' cmds=Commands 'od')
;

For:
	('for' expr=Expr 'do' cmds=Commands 'od')
;

While:
	('while' expr=Expr 'do' cmds=Commands 'od')
;

If:
	('if' expr=Expr 'then' cmdsTrue=Commands ('else' cmdsFalse=Commands)? 'fi')
;

Nop:
	nop='nop'
;

Affect:
	vars=Vars ':=' exp=Exprs
;

Vars:
	vars+=VARIABLE (',' vars+=VARIABLE)*
;

Exprs:
	exprs+=Expr (',' exprs+=Expr)*
;

Expr:
	=>expr=ExprAnd | expr=ExprSimple
;

ExprSimple:
	str='nil' | str=VARIABLE | str=SYMBOL | cons=Cons 
	| '(' 'list' listExpr=ListExpr ')' 
	| '(' 'hd' exprHd=Expr ')'
	| '(' 'tl' exprTl=Expr ')'
	| '(' strSymb=SYMBOL listExpr=ListExpr ')'
;

Cons:
	'(' 'cons' list=ListExpr ')'
;

ListExpr:
	exprs+=Expr (exprs+=Expr)*
;

ExprAnd:
	exprOr1=ExprOr ('and' exprOrX+=ExprOr)*
;

ExprOr:
	exprNot1=ExprNot ('or' exprNotX+=ExprNot)*
;

ExprNot:
	(not='not')? expr=ExprEq
;

ExprEq:
	(exprSimp1=ExprSimple '=?' exprSimp2=ExprSimple) | ('(' expr=Expr ')')
;


terminal VARIABLE:
	('A'..'Z') (('0'..'9') | ('a'..'z') | ('A'..'Z'))* ((('-' | '+' | '.' | '/' | '_' | '&') | '->') (('0'..'9') | ('a'..'z') | ('A'..'Z'))+)* ('?' | '!')?
;

terminal SYMBOL:
	('a'..'z') (('0'..'9') | ('a'..'z') | ('A'..'Z'))* ((('-' | '+' | '.' | '/' | '_' | '&') | '->') (('0'..'9') | ('a'..'z') | ('A'..'Z'))+)* ('?' | '!')?
;